plugins {
  id "java"
  id "org.springframework.boot" version "2.7.5"
  id "io.spring.dependency-management" version "1.1.0"

  // Code quality plugins
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "3.5.0.2730"
}

group = "uk.nhs.hee.tis.common"
version = "0.3.2"
sourceCompatibility = "11"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  // Spring Boot starters
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-validation"
  implementation 'com.amazonaws:aws-java-sdk-s3:1.12.376'
  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }

  // Lombok
  compileOnly 'org.projectlombok:lombok'
  testCompileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"
  testAnnotationProcessor "org.projectlombok:lombok"

  // MapStruct
  ext.mapstructVersion = '1.5.3.Final'
  implementation "org.mapstruct:mapstruct:$mapstructVersion"
  annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

  // Sentry reporting
  ext.sentryVersion = '6.9.0'
  implementation "io.sentry:sentry-spring-boot-starter:$sentryVersion"
  implementation "io.sentry:sentry-logback:$sentryVersion"

  implementation "net.datafaker:datafaker:1.7.0"
  implementation 'commons-fileupload:commons-fileupload:1.4'
  implementation 'commons-io:commons-io:2.11.0'
  implementation "commons-beanutils:commons-beanutils:1.9.4"
  implementation "org.apache.commons:commons-lang3:3.12.0"
}

checkstyle {
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled true
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"
    property "sonar.projectKey", "Health-Education-England_tis-common-upload"

    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}

bootRun {
  systemProperties System.properties
}

sourceSets {
  integrationTest {
    java.srcDir file('src/integrationTest/java')
    resources.srcDir file('src/integrationTest/resources')
    compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    runtimeClasspath += output + compileClasspath
  }
}

task integrationTest(type: Test) {
  useJUnitPlatform()
  description = 'Runs the integration tests.'
  group = 'verification'
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  mustRunAfter test
}

check.dependsOn integrationTest
