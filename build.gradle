plugins {
  id "java"
  id "org.springframework.boot" version "2.3.1.RELEASE"
  id "io.spring.dependency-management" version "1.0.9.RELEASE"

  // Code quality plugins
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "3.0"
}

group = "uk.nhs.hee.tis.common"
version = "0.0.1"
sourceCompatibility = "11"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  // Spring Boot starters
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-validation"
  implementation "com.amazonaws:aws-java-sdk-s3:1.11.817"
  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }

  // Lombok
  compileOnly "org.projectlombok:lombok:1.18.12"
  testCompileOnly "org.projectlombok:lombok:1.18.12"
  annotationProcessor "org.projectlombok:lombok:1.18.12"
  testAnnotationProcessor "org.projectlombok:lombok:1.18.12"

  // MapStruct
  implementation "org.mapstruct:mapstruct:1.3.1.Final"
  annotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"

  // Sentry reporting
  implementation "io.sentry:sentry-spring:1.7.30"

  implementation "com.github.javafaker:javafaker:1.0.2"
  implementation "commons-fileupload:commons-fileupload:1.3.1"
  implementation "commons-io:commons-io:2.4"
}

checkstyle {
  toolVersion = "8.29"
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled true
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"
    // TODO: Update sonar.projectKey to real value.
    property "sonar.projectKey", "Health-Education-England_tis-common-upload"

    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}

bootRun {
  systemProperties System.properties
}

sourceSets {
  integrationTest {
    java.srcDir file('src/integrationTest/java')
    resources.srcDir file('src/integrationTest/resources')
    compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    runtimeClasspath += output + compileClasspath
  }
}

task integrationTest(type: Test) {
  useJUnitPlatform()
  description = 'Runs the integration tests.'
  group = 'verification'
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  mustRunAfter test
}

check.dependsOn integrationTest
